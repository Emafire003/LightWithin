plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/lightwithin.accesswidener")
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		name = 'Ladysnake Mods'
		url = 'https://maven.ladysnake.org/releases'
		content {
			includeGroup 'io.github.ladysnake'
			includeGroup 'org.ladysnake'
			includeGroupByRegex 'dev\\.onyxstudios.*'
		}
	}

	maven { url 'https://maven.terraformersmc.com/' }

	exclusiveContent {
		forRepository {
			maven {
				url "https://cursemaven.com"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}

	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }

}

dependencies {

	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	//CardinalComponents (old version 4.0.1)
	include modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_components_version}")
	include modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cardinal_components_version}")
	//include modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-item:${project.cardinal_components_version}")
	//needed for CGL, adds 40kb
	include modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-scoreboard:${project.cardinal_components_version}")


	//Renderer
	//1.19 i think
	//include modImplementation( 'com.github.0x3C50:Renderer:9804cf7d99' )c4faf83
	//1.20.1
	//Not needed anymore as of 1.1.0
	//include modImplementation( 'com.github.0x3C50:Renderer:2273f5e562' )//2273f5e

	//StructurePlacerAPI
	include modImplementation("maven.modrinth:structureplacerapi:${project.structureplacerapi_version}")

	//CustomBrewRecipes
	include modImplementation("maven.modrinth:custombrewrecipes:${project.custombrewrecipes_version}")

	//ParticleAnimationLib
    include modImplementation("maven.modrinth:particleanimationlib:${project.pal_version}")

	//===========OPTIONAL DEPENDENCIES===========

	//ColoredGlowLib
	modImplementation "maven.modrinth:coloredglowlib:${project.coloredglowlib_version}"

	//For ALLIES & Enemies
	//Factions
	modImplementation "maven.modrinth:factions:${project.factions_version}"

	//Argonauts
	modCompileOnly "maven.modrinth:argonauts:${project.argonauts_version}"
	modCompileOnly "maven.modrinth:resourceful-lib:${project.resourcefullib_version}"
	//Which requires maybe: resourceful-lib

	//Open parties and claims aka OPAC
	modCompileOnly "maven.modrinth:open-parties-and-claims:${project.opac_version}"
	//Which require maybe: forge-config-api-port

	//Ftbteams
	modCompileOnly "curse.maven:ftbteams-438497:${project.ftbteams_version}"//home: https://www.curseforge.com/minecraft/mc-mods/ftb-teams-fabric

	//Stuff needed for ftbteams
	modCompileOnly "curse.maven:architectury-419699:4663009"//home: https://www.curseforge.com/minecraft/mc-mods/architectury-api
	modCompileOnly "curse.maven:ftblibrary-438495:4596510"//home: https://www.curseforge.com/minecraft/mc-mods/ftb-library-fabric

	//Permissions
	modCompileOnly "me.lucko:fabric-permissions-api:${project.fabricperms_version}"

	//Fixes
	//ReplayMod (to fix the runes rendering)
	modCompileOnly "maven.modrinth:replaymod:${project.replaymod_version}"

	//Flan
	modCompileOnly("maven.modrinth:flan:${project.flan_version}"){
		transitive = false
	}

	//Yet another world protector (world protection)
	//modImplementation("maven.modrinth:yawp:${project.yawp_version}")
	modCompileOnly("curse.maven:yawp-663276:${project.yawp_version}"){
		transitive = false
	}

	//Not really worth the effor at the moment
	//Leukocyte (world protection)
	//modImplementation("maven.modrinth:leukocyte:${project.leukocyte_version}")

	//Yet Another Config Lib YACL
	modImplementation "maven.modrinth:yacl:${project.yacl_version}"
	//modImplementation "dev.isxander.yacl:yet-another-config-lib-fabric:$project.yacl_version"

	//modmenu
	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 16
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
